(* Just for fun (for now). Axiomatize the notion of a time
   credit, and see what we can do with it. *)

abstract dollar : perm

(* We must pay one dollar at each function call. The type system
   does not do this by itself, so we have to be disciplined and
   call pay() before each function call. *)

val pay (| consumes dollar) : () =
  ()

(* Define the typical example of a FIFO queue implemented using two
   lists. The amortized complexity argument requires storing one
   credit in each cell of the first list. Note that this alone would
   make the type [fifo a] non-duplicable, even if we had not declared
   it mutable. We declare it mutable because there is nothing to lose
   and it is more convenient. *)

open list

data mutable fifo +a =
  F {
    front: list (a | dollar);
    back : list a;
  }

(* Creation and insertion. *)

val create [a] () : fifo a =
  F { front = nil; back = nil }

val insert [a] (consumes x: a, f: fifo a | consumes dollar) : () =
  f.front <- Cons { head = x; tail = f.front }
     (*  or: cons [(a | dollar)] (x, f.front) *)

(* The code for [rev_append] is the same as in the [list] library, but the
   type is different. This hints at a scalability problem... *)

val rec rev_append [a] (consumes xs: list (a | dollar), consumes ys: list a) : list a =
  if xs then begin
    pay(); (* it just works! *)
    rev_append (xs.tail, Cons { head = xs.head; tail = ys })
  end
  else
    ys

(* We can prove that [rev_append] returns a non-empty list if either
   of its arguments is a non-empty list. Unfortunately, this requires
   some code duplication! Let's do it anyway, for fun. *)

val rec rev_append2 [a] (
  consumes xs: list (a | dollar),
  consumes ys: Cons { head: a; tail: list a }
) : Cons { head: a; tail: list a } =
  if xs then begin
    pay(); (* it just works! *)
    rev_append2 (xs.tail, Cons { head = xs.head; tail = ys })
  end
  else
    ys

val rec rev_append1 [a] (
  consumes xs: Cons { head: (a | dollar); tail: list (a | dollar) },
  consumes ys: list a
) : Cons { head: a; tail: list a } =
  pay(); (* it just works! *)
  rev_append2 (xs.tail, Cons { head = xs.head; tail = ys })

(* Retrieval. *)

val rec retrieve [a] (f: fifo a | consumes dollar) : option a =
  if f.back then begin
    let x = f.back.head in
    f.back <- f.back.tail;
    some x
  end
  else if f.front then begin
    (* Reverse [f.front] and write it into [f.back]. *)
    pay();
    f.back <- rev_append1 (f.front, f.back);
    f.front <- nil;
    (* Now, I would like to call [retrieve] recursively, but I cannot do
       that, because [retrieve] will appear to require an infinite number
       of credits. A termination argument would be needed, which I don't
       know how to encode. So, I have to duplicate the code. Or, I could
       avoid duplication by defining an auxiliary function that expects
       a queue with a non-empty [back] component; but let's not. *)
    (* I used [rev_append1] instead of [rev_append] above, so we know
       that [f.back] must be non-empty. This saves a run-time test. *)
    let x = f.back.head in
    f.back <- f.back.tail;
    some x
  end
  else
    none

(* Now, let's define a dynamic counter of credits. This might be handy. *)

(* Because we don't (yet!) have arithmetic reasoning, I am defining the
   balance as a list of dollars, but in principle we could make it an integer
   n together with n dollars. TEMPORARY *)

(* [withdraw] allows retrieving one dollar, but it is not very useful,
   because we have to pay one dollar for calling [withdraw] anyway.
   We should either make [withdraw] a primitive function, or allow
   depositing and withdrawing more than one dollar at a time, or find
   some other solution to this problem. TEMPORARY *)

data mutable account =
  Account { balance: list (| dollar) }

val new_account () : account =
  Account { balance = nil }

val deposit (a: account | consumes dollar) : () =
  a.balance <- Cons { head = (); tail = a.balance }

val withdraw (a: account) : (| dollar) =
  match a.balance with
  | Nil ->
      (* Runtime failure: we have run out of credits. *)
      fail
  | Cons { tail } ->
      a.balance <- tail
  end
