open lock

(* A weak reference is a pair of a strong reference [r] and a lock
   that protects the permission [r @ ref a]. *)

alias wref a =
  (r: unknown, lock (r @ ref a))

(* Allocation. *)

val new [a] (consumes x: a) : wref a =
  let r = newref x in
  let l : lock (r @ ref a) = new () in
  (r, l)

(* Atomic swap. *)

val swap [a] (r: wref a, consumes y: a) : a =
  let x, l = r in
  acquire l;
  let v = !x in
  x := y;
  release l;
  v

(* Get. *)

(* One might be tempted to believe that a critical section is not
   needed here, since a single read (or write) instruction is atomic
   anyway. However, removing the acquire/release instructions here
   would be unsound (and ill-typed, of course). For instance, an
   attempt to [get] while an [update] is in progress would be
   dangerous. Thanks to the lock, such an attempt will block until
   [update] is finished. *)

val get [a] duplicable a => (r: wref a) : a =
  let x, l = r in
  acquire l;
  let v = !x in
  release l;
  v

(* Set. *)

val set [a] (r: wref a, consumes y: a) : () =
  let x, l = r in
  acquire l;
  x := y;
  release l

(* Update. *)

val update [a, b, p : perm] (r: wref a, f: (consumes (a | p)) -> (a, b) | consumes p) : b =
  let x, l = r in
  acquire l;
  let y, result = f !x in
  (* The test [if x != y] is not required. It may or may not save time.
     Anyway, it is interesting that the code is well-typed in its presence.
     In the branch where [x == y], this equality is used to transform the
     permission [y @ a] into [x @ a]. (I think!) *)
  if !x != y then
    x := y;
  release l;
  result

val borrow [a, b, p : perm] (r: wref a, f: (a | consumes p) -> b | consumes p) : b =
  let x, l = r in
  acquire l;
  let result = f !x in
  release l;
  result

(*
Local Variables:
compile-command: "../mezzo wref.mz"
End:
*)
