(* This module implements resizable arrays, also known as vectors,
   for short. *)

abstract vector +a
fact exclusive (vector a)

(* An array has a logical size -- the number of its elements -- and a
   physical length -- the length of the underlying array. The length
   is always greater than or equal to the size. The vector's size can
   be increased or decreased by the user. The vector's length is under
   the control of the implementation. *)

(* [create default] creates a new vector of size zero. The function [default]
   is stored, and will be later invoked by [resize] when the vector is
   enlarged in order to initialize the newly created slots. *)

val create: [a] (default: int -> a) -> vector a

(* [init (size, default, f)] creates a new vector of size [size]. The function
   [default] serves the same purpose as in [create] above. The function [f] is
   invoked immediately in order to initialize the [size] initial elements. *)

val init: [a] (size: int, default: int -> a, f: int -> a) -> vector a

(* [grab (r, default)] turns the array [r] into a vector of the same size.
   [default] serves the same purpose as in [create] above. *)

val grab: [a] (consumes r: marray a, default: int -> a) -> vector a

(* [list2vector (xs, default)] turns the list [xs] into a vector of the
   same size. [default] serves the same purpose as in [create] above. *)

val list2vector: [a] (consumes xs: list::list a, default: int -> a) -> vector a

(* [size v] returns the current size of the vector [v]. *)

val size: [a] vector a -> int

(* [resize (v, new_size)] increases or decreases the size of the vector [v]
   to [new_size]. If [new_size] is greater than the vector's current size,
   the function [f] that was supplied at vector creation time is invoked to
   initialize the new slots. *)

val resize: [a] (vector a, new_size: int) -> ()

(* [shrink (v, delta)] is a short-hand for [resize (v, size v - delta)].
   [delta] must be comprised between 0 and [size v], inclusive. *)

val shrink: [a] (vector a, int) -> ()

(* [pop v] is a short-hand for [shrink (v, 1)]. *)

val pop: [a] vector a -> ()

(* [get (v, i)] reads the element at index [i] in the vector [v].  Because the
   value is copied, the type [a] must be duplicable.  The index [i] must be
   comprised between 0, inclusive, and [size v], exclusive. *)

val get: [a] duplicable a => (vector a, int) -> a

(* [set (r, i, x)] sets the element at index [i] in the vector [v] to the
   value [x]. The index [i] must be comprised between 0, inclusive, and [size
   v], exclusive. *)

val set: [a] (vector a, int, consumes a) -> ()

(* [push (v, x)] appends the element [x] at the end of the vector [v]. *)

val push: [a] (vector a, consumes a) -> ()

(* [last v] returns the last element in the vector [v], that is, the element
   at index [size v - 1]. *)

val last: [a] duplicable a => vector a -> a

(* [sort (v, cmp)] sorts the elements of the vector [v] according to the
   ordering [cmp]. *)

val sort: [a, p : perm] duplicable a => (v: vector a, cmp: (a, a | p) -> int | p) -> ()

