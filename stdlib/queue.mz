(* FIFO structure, adapted from queue.ml *)

mutable data cell a =
  Cell { value: a; next: dynamic }

(* The fifo structure is implemented with a cyclic list,
   with a pointer on the last element. An integer gives the
   length of the structure. The integer is extensively used
   in the ML version to test whether the structure is empty.
   Here a tag will do the trick in most cases.
*)

mutable data fifo a =
  Empty    { length: int;  tail: ()      }
| NonEmpty { length: int;  tail: dynamic }
adopts cell a

(* TEMPORARY this code is rejected because "adopts" clauses in
   structural types are not yet supported by the parser.
(* This internal function finds the head of a nonempty queue. *)

val head [a, tail : term] (q: NonEmpty { length: int; tail = tail } adopts cell a | tail @ dynamic) : dynamic =
  taking tail from q begin
    tail.next
  end
*)

val create [a] (): fifo a =
  Empty { length = 0;  tail = () }

val length [a] (q: fifo a): int =
  match q with
  | Empty -> 0
  | NonEmpty -> q.length
  end

(* The function clear doesn't type without the match *)

val clear [a] (q: fifo a): () =
  match q with
  | Empty -> ()
  | NonEmpty ->
      tag of q <- Empty;
      q.length <- 0;
      q.tail   <- ()
  end

(* Rotate a queue [q] by moving its head element to the tail. *)

val rotate [a] (q: fifo a): () =
  match q with
  | Empty ->
      ()
  | NonEmpty { tail } ->
      taking tail from q begin
	q.tail <- tail.next
      end
  end

(* Insert an element [x] at the beginning of a queue [q]. *)

val insert_head [a] (consumes x: a, q: fifo a): () =
  let c = Cell {
    value = x; next = ()
  } in
  match q with
  | Empty ->
      c.next <- c;
      give c to q;
      tag of q <- NonEmpty;
      q.length <- 1;
      q.tail <- c
  | NonEmpty { tail } ->
      taking tail from q begin
        c.next <- tail.next;
        tail.next <- c;
      end;
      give c to q;
      q.length <- q.length +1
  end
  
(* Insert an element [x] at the end of a queue [q]. *)

val insert [a] (consumes x: a, q: fifo a): () =
  insert_head (x, q);
  rotate q

(* Peek: the element has to be duplicable. *)

val peek [a] duplicable a => (q: fifo a): option a =
  match q with
  | Empty -> 
      none
  | NonEmpty { tail } ->
      let head =
	taking tail from q begin
	  tail.next
	end
      in
      taking head from q begin
	some head.value
      end
  end

(* Retrieve the head element of a queue [q]. *)

val retrieve [a] (q: fifo a): option a =
  match q with
  | Empty ->
      None
  | NonEmpty { tail } ->
      take tail from q; (* TEMPORARY why is there an "exclusive resource allocation conflict" here? *)
      let head = tail.next in
      if head == tail then begin
	(* Interesting BUG: if we change == to != above, we obtain an error:
	   exception Failure("Two matching permissions? That's strange...") *)
	tag of q <- Empty;
	q.length <- 0;
	q.tail <- ()
      end
      else begin
	q.length <- q.length - 1;
	take head from q;
	tail.next <- head.next;
	give tail to q
      end;
      (* At this point, we own the [head] cell. Note that this annotation
	 is not required. *)
      assert head @ cell a;
      (* The head cell must not be given back to the queue. We need to assert
	 the ownership of [head.value], whose type is not duplicable. *)
      some head.value
 end

(* In contrast with [list::map], the function [queue::map] cannot perform
   a strong update on the queue elements. The queue [q] adopts elements of
   type [cell a], and this cannot be changed. Thus, [queue::map] must require
   its argument [f] to preserve the type [a] of the list element. *)
(* Possible variant: with a loop on the length of the queue [q], rebuilding
   [q] can be done using [queue::insert] instead of [queue::insert_head]. *)


val map [a, b, p:perm] (f: (a | p) -> b, q: fifo a | p): fifo b =

  let rec map (q: fifo a, consumes accu: fifo b | p): fifo b =
    (* The call to retrieve takes the head value from the queue [q], and
       removes the head cell of [q]. *)
    let a = retrieve q in
    match a with

    | None ->
        accu

    | Some { contents = a } ->
        let b = f a in
	insert (b, accu);
	(* Since the head cell of the queue [q] has been removed, the
	   auxiliary [map] can be called again on [q]. *)
	let q' = map (q, accu) in
	(* To rebuild the original [q] we have to insert the retrieved value
	   at the beginning of [q] rather than its end. *)
	insert_head (a, q);
	q'

    end
  in
  
  let accu = create [b] () in
  map (q, accu)

val copy [a] duplicable a => (q: fifo a): fifo a =
  map ((fun (x:a): a = x), q)

(* In contrast with [list::fold], the function [queue::fold] cannot perform
   a strong update on the queue elements. The queue [q] adopts elements of
   type [cell a], and this cannot be changed. Thus, [queue::fold] must require
   its argument [f] to preserve the type [a] of the list element. *)

val fold [a, b, p:perm] (
  q: fifo a,
  consumes acc: b,
  f: (a, consumes b | p) -> b
| p
): b =
  
  match q with
  | Empty ->
      acc

  | NonEmpty { tail } ->

      let rec fold (consumes acc: b, c: dynamic | q @ fifo a * p): b =
	take c from q;
        let Cell { next; value } = c in
        let acc = f (value, acc) in
	give c to q;
        if tail == c then
          acc
        else
          fold (acc, next)
      in

      (* Obtain the address of the first cell. *)
      let head = taking tail from q begin tail.next end in
      (* We're ready to launch the recursion. *)
      fold (acc, head)

  end

val iter [a, p:perm] (q: fifo a, f: (a | p) -> () | p) : () =
  fold (q, (), fun (x:a, () | p): () = f x) 

(*
Local Variables:
compile-command: "../mezzo queue.mz"
End:
*)
